package ImageRenderer

public class ImageRenderer
  private image array[100] images
  private int pointer = 0
  private vec2 initPosition
  private int team
  private int offset

  construct(vec2 initialPosition, int playerTeam, int imageOffset)
    initPosition = initialPosition
    team = playerTeam
    offset = imageOffset

  function add(vararg string imgs)
    for img in imgs
      let i = renderImage(img, vec2(initPosition.x + offset * pointer, initPosition.y))
      saveImage(i)

  function clear()
    for i = 0 to pointer - 1
      clearImageByIndex(i)
    pointer = 0

  private function clearImageByIndex(int index)
    if images[index] != null
      removeImage(images[index])
      images[index] = null

  private function saveImage(image img)
    images[pointer] = img
    pointer++

  private function removeImage(image img)
    if (img != null)
      ShowImage(img, false)
      img.remove()

  private function renderImage(string imgPath, vec2 coord) returns image
    let img = createImage(imgPath, coord, 100, 100)
    img.show()
    setImageVisibility(img)
    return img

  private function setImageVisibility(image img)
    if GetLocalPlayer().getTeam() == team or IsPlayerObserver(GetLocalPlayer())
      ShowImage(img, true)                    
    else
      ShowImage(img, false)                      
  